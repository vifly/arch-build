# Maintainer: Shapiro <shapiro at quantentunnel dot de>

_pkgname='VeraCrypt'
pkgname='veracrypt-updated'
pkgver='1.26.7'
pkgrel='1'
pkgdesc='Disk encryption with strong security based on TrueCrypt'
arch=('aarch64' 'arm64' 'armhf' 'i686' 'x86_64')
url='https://www.veracrypt.fr'
license=("LicenseRef-${_pkgname}")
depends=('device-mapper' 'fuse2>=2.8.0' 'wxwidgets-common>=3.2.4' 'wxwidgets-gtk3>=3.2.4')
makedepends=('pcsclite' 'yasm')
optdepends=('sudo: mounting encrypted volumes as nonroot users' 'pcsclite: providing support for smartcards')
provides=('veracrypt')
conflicts=('veracrypt')
source=("${url}/code/VeraCrypt/snapshot/VeraCrypt_${pkgver}.tar.gz")
sha512sums=('c803c6301a8a7b6d8efc74284070c01629a760dceaa518f2287d62709bd25450fec75f2fa0e2ab0eb57993faec9cba4d51104273a56a88fa288a1e14bcf868bd')

build() {
  cd "${_pkgname}_${pkgver}/src"
  make PKG_CONFIG_PATH='/usr/lib/pkgconfig' WX_CONFIG='/usr/bin/wx-config' TC_EXTRA_LFLAGS="${LDFLAGS}" TC_EXTRA_CXXFLAGS="${CXXFLAGS} ${CPPFLAGS}" TC_EXTRA_CFLAGS="${CFLAGS} ${CPPFLAGS}"
}

package() {
  cd "${_pkgname}_${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -Dm 0644 License.txt "${pkgdir}/usr/share/licenses/veracrypt/License"
  rm "${pkgdir}/usr/bin/veracrypt-uninstall.sh"
  rm "${pkgdir}/usr/sbin/mount.veracrypt"
  rm "${pkgdir}/usr/share/doc/veracrypt/License.txt"
}
