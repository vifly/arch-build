# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

_pkgname='ion'
pkgname='ion-git'
pkgver='r1869.9bc52a5'
pkgrel='1'
pkgdesc='The Ion Shell. Compatible with Redox and Linux. (Git version)'
arch=('i686' 'x86_64')
url='https://gitlab.redox-os.org/redox-os/ion'
license=('MIT')
makedepends=('git' 'rust')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
install='ion-git.install'
source=("git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${_pkgname}"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${_pkgname}"
    cargo build --frozen --release
    make manual
}

check() {
    cd "${_pkgname}"
    DEBUG=1 RUSTUP=0 make tests
}

package() {
    cd "${_pkgname}"
    install -Dm 755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin"
    install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"
    install -dm 755 "$pkgdir/usr/share/doc/$_pkgname"
    find manual/builtins \
         -maxdepth 1 \
         -name '*.1' \
         -type f \
         -execdir install -Dm 644 -t "${pkgdir}/usr/share/${_pkgname}/man/man1" "{}" +
    cp -r manual "$pkgdir/usr/share/doc/$_pkgname"
    rm -r "${pkgdir}/usr/share/doc/${_pkgname}/manual/builtins"
}
