# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

_pkgname='ion'
pkgname="${_pkgname}-git"
pkgver='r1869.9bc52a5'
pkgrel='1'
pkgdesc='The Ion Shell. Compatible with Redox and Linux. (Git version)'
arch=('i686' 'x86_64')
url="https://gitlab.redox-os.org/redox-os/${_pkgname}"
license=('MIT')
makedepends=('git' 'rust')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
options=('!lto')
install="${pkgname}.install"
source=("git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${_pkgname}"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${_pkgname}"
    mkdir manual/builtins
    cargo build --frozen --release --features man,unicode
    echo -n "# Builtin commands" > manual/src/builtins.md
	for man in manual/builtins/*; do \
		echo "" >> manual/src/builtins.md; \
		echo -n "## " >> manual/src/builtins.md; \
		cat "${man}" >> manual/src/builtins.md; \
	done
}

check() {
    cd "${_pkgname}"
    bash tests/run_examples.sh
    cargo test --workspace --features man,unicode
}

package() {
    cd "${_pkgname}"
    install -Dm 0755 "target/release/${_pkgname}" -t "${pkgdir}/usr/bin"
    install -Dm 0644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"
    install -dm 0755 "${pkgdir}/usr/share/doc/${_pkgname}"
    find 'manual/builtins' -maxdepth 1 -name '*.1' -type f -execdir install -Dm 644 -t "${pkgdir}/usr/share/${_pkgname}/man/man1" "{}" +
    cp -r manual "${pkgdir}/usr/share/doc/${_pkgname}"
    rm -r "${pkgdir}/usr/share/doc/${_pkgname}/manual/builtins"
}
