# Maintainer: loserMcloser <reebydobalina@gmail.com>

_pkgname='yt-dlp-ChromeCookieUnlock'
pkgname='yt-dlp-plugin-chromecookieunlock'
pkgver='2024.04.29'
pkgrel='1'
pkgdesc="A yt-dlp plugin to unlock the cookie database of chromium-based browsers."
arch=('any')
url="https://github.com/seproDev/${_pkgname}"
license=('MIT')
depends=('python' 'yt-dlp')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-hatchling')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('eceb682a4897f251cb36cbe2a2ee629768daacb2bf6f131173bddadf2620adbd3545e64076e22aaee9902ce6f30eeef48c9c60e99f7dae375bb0bd9ef77b9077')

build() {
	cd "${_pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${_pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl
 	install -Dm 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
