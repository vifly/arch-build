# Maintainer: Scheme Matrix <schemematrix (at) outlook (dot) com>

_pkgname=findutils
pkgname=uutils-$_pkgname
pkgver=0.4.2
pkgrel=1
pkgdesc="Rust implementation of findutils"
arch=('x86_64')
url='https://github.com/uutils/findutils'
license=('MIT')
makedepends=('clang15' 'rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('4bef3db72985158dd89ff7bd4022e264dff840981d72671b1deafad606b9cbe98d55860c1561c2833475f8021d47fddaac1913bd3c6e93bec76754b935629c7b')

prepare() {
  cd "$_pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$_pkgname-$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$_pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$_pkgname-$pkgver"
  for utility in {find,xargs}; do install -Dm 755 "target/release/$utility" "$pkgdir/usr/bin/uu-$utility"; done
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
